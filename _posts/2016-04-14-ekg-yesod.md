---
layout: post
title: Monitoring a Yesod app with EKG
---

> It's been a long time since I've worked on the design of this blog. It's honestly much prettier [as a gist](https://gist.github.com/MaxGabriel/4e56d74b9938428bdc81205dc6700ecf), if you'd rather read it there.

Monitoring is critical to a well-functioning production system. OS-level metrics like CPU load and RAM usage are a good start, but you'll eventually want to record application-level metrics like how long it's taking you to service each HTTP request, or how much time is spent in garbage collection. Once tracked, this data can be used by humans and machines (like monitoring systems) to gain insight into how your app is running.

In this post I'll show how you can use EKG, a popular Haskell metrics library, to add application-level metrics to your Yesod app.

## A Brief Overview of EKG Metric Types

EKG allows reporting four different types of metrics from your app:

### Counters

A [`Counter`](https://www.stackage.org/lts-5.11/hoogle?q=System.Metrics.Counter&exact=on) is an integer metric that you can only increment or add to, and probably the most common one you'll use. Example use cases:

* A counter that increments each time a request 500s lets you track exceptions. Too many 500s per minute might trigger an alert.
* A counter that increments each time you insert a row into a database would help you track why your database is under load.

### Gauges

[`Gauge`s](https://www.stackage.org/lts-5.11/hoogle?q=System.Metrics.Gauge&exact=on) record a value that can go up and down. Unlike a counter, which records a one-off event like a request being made, a gauge implies the reported value will persist. Examples:

* A gauge might track the amount of jobs in a queue. If the gauge height is rising, you know you're not processing the queue quick enough.
* A gauge might track the number of concurrent connections. You could monitor this value to make sure it doesn't get too close to your maximum connection limit.

<div align="center">
  <figure>
    <img width="188" height="188" src="http://maxgabriel.github.io/images/ekg/pressureGauge.png" alt="Pressure Gauge">
    <img width="87" height="188" src="http://maxgabriel.github.io/images/ekg/thermometer.png" alt="Thermometer picture">
    <img width="188" height="188" src="http://maxgabriel.github.io/images/ekg/mphGauge.png" alt="MPH Gauge">
    <figcaption><br>Real life gauges. <em>Left to right:</em> pressure gauge, thermometer/SCM tool, speedometer</figcaption>
  </figure>
</div>

### Labels

[Labels](https://www.stackage.org/lts-5.11/hoogle?q=System.Metrics.Label&exact=on) are an arbitrary string value your app can report. I don't have much experience with labels. Example use case:

* A label might report the git SHA hash of the running application. This allows you to track if an old version of your code is running by mistake.

### Distributions

A [`Distribution`](https://www.stackage.org/lts-5.11/hoogle?q=System.Metrics.Distribution&exact=on) tracks a series of values, then computes statistical information about them. Examples should help illustrate:

* A web server might record the amount of time serving each request. The mean could be tracked to see if page serving is too slow.
* An ad network charging different amounts per ad impression might record each one. The count of events would tell them how many impressions they're processing (like a counter), and the mean would tell them their average charge price.
* A `Distribution` might track the size of cookies in bytes. A high `max` value could warn you that your cookies are approaching the 4K size limit.

[StatsD](https://github.com/etsy/statsd), Etsy's Node.js daemon for tracking metrics, calls this a Timer (even though it has many more use cases). Unfortunately, EKG doesn't track percentiles like StatsD does, which would allow you to see e.g. the 99th percentile response time, filtering out the 1% of outliers. (If you're interested in contributing, there's [an issue](https://github.com/tibbe/ekg-core/issues/11) open to implement this).

### Viewing these Metrics

Now that you have a brief overview of the types of metrics EKG can track, we'll see how it reports them to you via its built-in HTTP server.

## Adding EKG

Start by adding `ekg` and `ekg-core` to the `build-depends` section of your `.cabal` file. Then just add `forkServer "localhost" 8000` before you start your actual Yesod server in `getApplicationDev`:

```haskell
-- Application.hs
import qualified System.Remote.Monitoring as EKG

getApplicationDev :: IO (Settings, Application)
getApplicationDev = do
    settings <- getAppSettings
    foundation <- makeFoundation settings

    EKG.forkServer "localhost" 8000

    ...
```

> Note: The `main` function used for development is `develMain`, which calls into `getApplicationDev`. In production you'll use `appMain`, but we'll stick to the development version for this guide.

`forkServer` will start up a metrics server with the default metrics (just Garbage Collection statistics). Open [http://localhost:8000](http://localhost:8000) to check it out:

<div align="center">
  <img width="552" height="417" src="http://maxgabriel.github.io/images/ekg/EKG2.png">
</div>

> If your Garbage Collection statistics are all 0, you'll need to tell the Haskell runtime system (RTS) [to collect GC statistics](http://hackage.haskell.org/package/ekg-0.4.0.9/docs/System-Remote-Monitoring.html#g:1) (As far as I can tell this came by default with `yesod devel`).

EKG will also give you [JSON output](https://gist.github.com/MaxGabriel/3f684ec2ed253a7816d6330d7a434397) if you change your `Accept` Header, allowing machines to parse the data:

```bash
curl -H "Accept: application/json" http://localhost:8000/
```

## Adding More Metrics

GC metrics are great, but we'd like to register metrics specific to our app. EKG keeps metrics in a [`Store`](https://www.stackage.org/lts/hoogle?q=System.Metrics.Store&exact=on) and we'll need one to do things like create counters. We'll want to access it across our application, so we'll follow the general Yesod pattern of adding it to our `App` datatype. We'll also create a record, `Metrics` to hold the specific counters/gauges/etc. we make:

```haskell
-- Foundation.hs
import qualified System.Metrics as EKG
import System.Metrics.Counter (Counter)

data Metrics = Metrics
    { homeRequestCounter :: Counter
    }

data App = App
    { appSettings     :: AppSettings
    ... -- other Fields
    , appMetricsStore :: EKG.Store
    , appMetrics      :: Metrics
    }
```

Then in `makeFoundation`, where we create our `App`, we'll create those instances, using [`RecordWildCards`](https://ocharles.org.uk/blog/posts/2014-12-04-record-wildcards.html) to make it easy to fill out the fields:

```haskell
-- Foundation.hs
import qualified System.Remote.Monitoring as EKG
import qualified System.Metrics as EKG

makeFoundation :: AppSettings -> IO App
makeFoundation appSettings = do
  -- Create the store
    appMetricsStore <- EKG.newStore
    -- When you create your own store you need to register GC metrics manually:
    EKG.registerGcMetrics appMetricsStore
    -- Create the individual counters/gauges/etc. we want to track here:
    homeRequestCounter <- EKG.createCounter "home.requests" appMetricsStore
    let appMetrics = Metrics {..}    
    
    ... -- Other App initialization here
    
    let mkFoundation appConnPool = App {..}
```

Now that we're using a custom store, we need to tweak how we launch the EKG server to use that store:

```haskell
-- Foundation.hs
import qualified System.Remote.Monitoring as EKG

getApplicationDev :: IO (Settings, Application)
getApplicationDev = do
    settings <- getAppSettings
    foundation <- makeFoundation settings

    _ <- EKG.forkServerWith (appMetricsStore foundation) "localhost" 8000
```

Now that we can access our individual metrics from our `App`, we can use them in our `Handler`s. Here's how we'd retrieve a `Counter` and increment it:
    
```haskell
-- Home.hs
import qualified System.Metrics.Counter as Counter

getHomeR :: Handler Html
getHomeR = do
    app <- getYesod
    liftIO $ Counter.inc $ homeRequestCounter $ appMetrics app
```

After recompiling, hit your homepage with a bunch of refreshes, then head back to [http://localhost:8000](http://localhost:8000) to find your new metric in the sidebar. Click the little chart icon to graph the metric.

<div align="center">
  <img width="340" height="205" src="http://maxgabriel.github.io/images/ekg/MetricsSidebar.png">

  <img width="671" height="338" src="http://maxgabriel.github.io/images/ekg/HomeRequestGraph.png">
</div>

## Sending Metrics to Graphite

EKG's web frontend is convenient for trying out the library, but it's not ideal for a production system:

* EKG doesn't store historical metrics data. For a production system you'd want to store your metrics to disk so you can view data across restarts of your app, going back several weeks at least.
* EKG doesn't aggregate counters across multiple servers, which you may want to horizontally scale.
* The web frontend is pretty basic. It'd be nice to have customizable graphs and dashboards.
* EKG is Haskell-specific. You'll want to also store operating system metrics, like CPU load and RAM usage, with the rest of your data.

So instead of storing our metrics locally, we'll send them to a database. [Graphite](https://graphite.readthedocs.org/en/latest/faq.html) is something of an industry standard for storing metrics data and interfaces with many other systems, so we'll use that. Go install Graphite now if you're following this as a guide.

### ekg-carbon

We'll send metrics to Graphite using the [`ekg-carbon`](https://hackage.haskell.org/package/ekg-carbon) package.

> Unfortunately `ekg-carbon` needs a dependency bump to work with `vector-0.11.0.0`, the version in LTS Haskell 5.11. I cloned [`ekg-carbon`](https://github.com/ocharles/ekg-carbon), bumped the vector constraint as shown [in this PR](https://github.com/ocharles/ekg-carbon/pull/6/files), added `import Prelude` to `System.Remote.Monitoring.Carbon`, and added `ekg-carbon` to the `packages` array in my `stack.yaml` file. Later I also deleted the `Setup.hs` file in `ekg-carbon` since `wai-devel` was complaining about a second `main` function being present.

Once you have `ekg-carbon` installed, replace `forkServerWith` with `forkCarbonRestart`. The `defaultCarbonOptions` will work for a standard carbon install on localhost. The lambda is an exception handler function for errors sending metrics to Carbon. The lambda simply prints the error and restarts (I think the data is lost in this case, but that's usually fine for metrics, and even a design goal of systems like StatsD).

```haskell
-- Application.hs
import System.Remote.Monitoring.Carbon

getApplicationDev :: IO (Settings, Application)
getApplicationDev = do
    settings <- getAppSettings
    foundation <- makeFoundation settings

    let store = appMetricsStore foundation
    _threadId <- forkCarbonRestart defaultCarbonOptions
                                   store
                                   (\ex restart -> do hPutStrLn stderr ("ekg-carbon: " ++ show ex)
                                                      restart)
```

Once again, restart your Yesod app and hit your homepage with some requests. You should be able to see the request data tracked in Graphite on the sidebar:

<div align="center">
  <img align="center" width="299" height="491" src="http://maxgabriel.github.io/images/ekg/GraphiteSidebar.png">
</div>

Click on the metric to graph it (Note: over such a small timescale, your metric may not graph very well)

<div align="center">
  <img width="293" height="154" src="http://maxgabriel.github.io/images/ekg/HomeRequestGraphite.png">
</div>

If you're happier with your data in Graphite, you can also remove the `ekg` package entirely and just depend on `ekg-core`, avoiding the large `snap-server` dependency of `ekg`. You'll just need to replace imports of `System.Remote.Monitoring` with `System.Metrics`.

### Side Note: Pulling data from Graphite

#### Prettier Graphs

Graphite's interface is very, uh, Windows 95-ish. I actually really like it for exploring metrics, but if you want to create nice dashboards I recommend something like [Grafana](grafana.org/), which can query Graphite and generate pretty, configurable graphs.

#### Monitoring

You can also monitor your app by making queries to Graphite. I don't have much experience in monitoring solutions, but at work we use [Sensu](https://sensuapp.org/) to do monitoring.

## Conclusion

Adding EKG to Yesod is very simple and probably worth doing for most Yesod apps. It costs you very little to add `forkServer "localhost" 8000` to start tracking GC stats, and tracking application-specific metrics isn't much more difficult. If you aren't already, give EKG a shot and add it to your Yesod app.

### Special thanks

* Thanks to [Micah Fivecoate](https://github.com/m5) for reviewing this post multiple times. He found a bug despite no prior Haskell experience :)
* Thanks to [Johan Tibbe](https://github.com/tibbe), author of EKG, for reviewing this post and making several recommendations.

##### Photo Credit:
* Mercury Thermometer by Anonimski - Own work, CC BY-SA 3.0, [Details](https://commons.wikimedia.org/w/index.php?curid=29726755). 
* Speedometer by Brian Snelson from Hockley, Essex, England - Speedometer, CC BY 2.0, [Details](https://commons.wikimedia.org/w/index.php?curid=3598465)